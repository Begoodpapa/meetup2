*Setting*							
Suite Setup	Connect to Target Hardware and create Test resource						
Suite Teardown	disconnect from target hardware						
Force Tags	Product_Backlog_ID-B01455	Iteration_ID-I01834	Iteration_ID-I01256	PRS_ID-IL_PRS_4375	PRS_ID-PRS_4134	PRS_ID-PRS_4207	PRS_ID-IL_PRS_4419
...	PRS_ID-PRS_4129	PRS_ID-IL_PRS_4416	PRS_ID-PRS_4144	EFS_ID-RNC_EFS_1886_53	EFS_ID-RNC_EFS_1886_61	EFS_ID-RNC_EFS_1886_55	EFS_ID-RNC_EFS_1886_86
...	EFS_ID-RNC_EFS_1886_56	EFS_ID-RNC_EFS_1886_57	EFS_ID-RNC_EFS_1886_58	EFS_ID-RNC_EFS_1886_88	EFS_ID-RNC_EFS_1886_47	hw_env-RNC	hw_env-RNC_S0
...	hw_env-RNC_S1	type-FUT	exec_type-automatic	element-IL	release-IL3	owner-I_EXT_NWS_WCDMA_RA_RI_HZSWD_LITE_FLASH@internal.nsn.com	exec_level-pre
...	HW_TYPE-BCN_A1	BCN_STEP-BCN_S0	QL-7				
Resource	il.html						
Resource	./resources/common_operation_resource.html						
Resource	./resources/IPBR_basic_operation_resource.html						
Resource	./resources/cac_basic_operation_resource.html						
Library	Collections						
							
*Variable*							
${IPBR_ID_4017}	4017						
${IPBR_ID_4018}	4018						
${IPBR_NAME_4017}	BTS-4017						
${IPBR_NAME_4018}	BTS-4018						
${MUX_LOCAL_PORT_53999}	53999						
${MUX_LOCAL_PORT_53000}	53000						
${UDP_PORT_53999}	53999						
${PORT_53999_HEX}	D2EF						
${MUX_REMOTE_PORT_65534}	65534						
${MUX_FLAG_ENABLE}	enable						
${MUX_UDP_DSCP_25}	25						
${MUX_UDP_DSCP_35}	35						
${MUX_UDP_DSCP_25_HEX}	19						
${MUX_PKT_NUM_5}	5						
${TEST_IP_ADDRESS_1}	10.10.10.10						
${TEST_IP_ADDRESS_2}	10.10.10.20						
${IPBR_ID_1}	1						
${USCP_0}	USCP-0						
${BITRATE_10000}	10000						
${BITRATE_11000}	11000						
${BITRATE_12000}	12000						
${BITRATE_13000}	13000						
${BITRATE_14000}	14000						
${TWO_GROUP}	2						
${EME_PARA}	QPRI=255						
							
*Test Case*							
CAC support MUX call creation without traffic	[Tags]	ATC_ID-TT_CAC.FUT.001.AT.I01834	ct-positive				
	Create IPBR and IPRO with multiplexing parameters successfully						
	Check the MuX port should be active in CACPRB						
	Modify IPBR with multiplexing parameter successfully						
	Check the MuX port should be updated in CACPRB						
	[Teardown]	release IPRO and IPBR for the test successfully					
							
Transport Bearers Bandwidth calculation for Multiplexing Port Setting	[Tags]	ATC_ID-TRM_CAC.FUT.001.AT.I01256	ct-positive	FRS_ID-PCM_100201_11_R1886			
	[Setup]	reserve IPRO with IPBR mux-setting enable and RTP leg for the test					
	${iub_info}	Reserve IuB leg with CAC enable, and the DSCP attribute of the request equals the mux-dscp value of the IPBR_4017					
	update IuB leg bandwidth with multiplexing IP enabled	${iub_info}					
	Reserve IuB leg with CAC enable, and the DSCP attribute of the request does not equal the mux-dscp value of the IPBR_4017						
	Reserve RTP leg with CAC enable, and the DSCP attribute of the request equals the mux-dscp value of the IPBR_4017						
	[Teardown]	release test resource created in the test case					
							
Transport Bearers Port allocation for Multiplexing Port Setting	[Tags]	ATC_ID-TRM_CAC.FUT.002.AT.I01256	ct-negative				
	[Setup]	reserve IPRO with IPBR mux-setting enable and RTP leg for the test					
	register expect error syslog						
	Reserve an IuB leg with the request port equals to the IPBR_4017's local-mux-port						
	Update the IPBR_4017's local-mux-port						
	check the original local-mux-port in IP buffer						
	${result}	inquiry_udp_conn_port_info	${IP_GROUP_1.ip_addr}	${MUX_LOCAL_PORT_53000}			
	should be equal	${result['${IP_GROUP_1.ip_addr}:${MUX_LOCAL_PORT_53000}']['type']}	BUFFERED				
	#Reserve an IuB leg with the request port equals to the IPBR_4017's original local-mux-port						
	reserve an emergency Iub leg with the request port equal to the IPBR_4017's original local-mux-port						
	[Teardown]	release test resource created in the test case					
							
SCLI Operation for the Multiplexing Port Setting (IPBR and IP 1 - 2 mapping)	[Tags]	ATC_ID-TRM_CAC.FUT.003.AT.I01256	ct-negative				
	[Setup]	reserve IPRO with IPBR mux-setting enable and RTP leg for the test					
	Reserve Iub Leg with IPBR_4017, and the starting port and maximum port of the request equals to PORT_53999						
	Update the IPBR_4017's local-mux-port from MUX_PORT_53000 to MUX_PORT_53999 with normal mode						
	Update the IPBR_4017's local-mux-port from MUX_PORT_53000 to MUX_PORT_53999 with forced mode						
	Reserve an IPRO with IPBR_4017 and TEST_IP_ADDRESS_2						
	[Teardown]	release all IPRO with IPBR 4017 created in the test case					
							
SCLI Operation for the Multiplexing Port Setting (IPBR and IP 2 - 1 mapping)	[Tags]	ATC_ID-TRM_CAC.FUT.004.AT.I01256	ct-negative				
	[Setup]	Reserve 2 IPBRs and 1 RTP leg for the test					
	Reserve Iub Leg with IPBR_4017, and the starting port and maximum port of the request equals to PORT_53999						
	Reserve an IPRO with IPBR_4018 and TEST_IP_ADDRESS_1 with normal mode						
	Reserve an IPRO with IPBR_4018 and TEST_IP_ADDRESS_1 with forced mode						
	[Teardown]	Release 2 IPBRs related IPRO and 1 RTP leg in the test					
							
*Keyword*							
check specified call should be released	${result}	SCLI_show_call_resource	${USCP_0}	${RTP_LEG.hand_id}	${RTP_LEG.hand_focus}		
	should be equal	${result.result}	TIMEOUT				
							
release all IPRO with IPBR 4017 created in the test case	release legs created in test case						
	release_ipro_with_specified_ipbr_id	${IPBR_ID_4017}					
	Delete IPBR successfully	${IPBR_ID_4017}					
							
Reserve an IPRO with IPBR_4018 and TEST_IP_ADDRESS_1 with normal mode	${result}	add ipro with specified parameters	${IPBR_ID_4018}	${IP_GROUP_1.ip_addr}	${IP_GROUP_1.rg_name}	${IP_GROUP_1.iface}	mode=normal
	should be equal	${result['IPRO Add']}	Fail				
							
Reserve an IPRO with IPBR_4018 and TEST_IP_ADDRESS_1 with forced mode	${result}	add ipro with specified parameters	${IPBR_ID_4018}	${IP_GROUP_1.ip_addr}	${IP_GROUP_1.rg_name}	${IP_GROUP_1.iface}	mode=forced
	should be equal	${result['IPRO Add']}	Success				
	check specified call should be released						
							
Reserve 2 IPBRs and 1 RTP leg for the test	Add IPBR successfully	${IPBR_ID_4017}	${IPBR_NAME_4017}	${ROUTE_BW_DEFAULT}	${COMMIT_BW_DEFAULT}	${COMMIT_SIG_BW_DEFAULT}	${COMMIT_DCN_BW_DEFAULT}
	...	${IFC_NRTHSDPA_E_RED}	${IFC_NRTHSDPA_E_RED}	${SCHEDULE_TYPE_DEFAULT}	${PHB_PROFILE_0}	${DSPM_PROFILE_0}	${MUX_FLAG_ENABLE}
	...	${MUX_PKT_NUM_5}	${MUX_LOCAL_PORT_53000}	${MUX_REMOTE_PORT_65534}	${MUX_UDP_DSCP_25}		
	Add IPBR successfully	${IPBR_ID_4018}	${IPBR_NAME_4018}	${ROUTE_BW_DEFAULT}	${COMMIT_BW_DEFAULT}	${COMMIT_SIG_BW_DEFAULT}	${COMMIT_DCN_BW_DEFAULT}
	...	${IFC_NRTHSDPA_E_RED}	${IFC_NRTHSDPA_E_RED}	${SCHEDULE_TYPE_DEFAULT}	${PHB_PROFILE_0}	${DSPM_PROFILE_0}	${MUX_FLAG_ENABLE}
	...	${MUX_PKT_NUM_5}	${MUX_LOCAL_PORT_53999}	${MUX_REMOTE_PORT_65534}	${MUX_UDP_DSCP_25}		
	Add IPRO successfully	${IPBR_ID_4017}	${IP_GROUP_1.ip_addr}	${IP_GROUP_1.rg_name}	${IP_GROUP_1.iface}		
	create leg list						
	${rtp_leg_info}	create rtp leg	${USCP_0}	${IPBR_ID_1}	NCAC=1		
	should be equal	${rtp_leg_info.result}	SUCCESSFUL				
	Append to List	${OUT_LEG_LIST}	${rtp_leg_info}				
	set test variable	\${CALL_ID}	${rtp_leg_info.call_id}				
	set test variable	\${RTP_LEG}	${rtp_leg_info}				
							
release 2 IPBRs related IPRO and 1 RTP leg in the test	release legs created in test case						
	release_ipro_with_specified_ipbr_id	${IPBR_ID_4017}					
	release_ipro_with_specified_ipbr_id	${IPBR_ID_4018}					
	Delete IPBR successfully	${IPBR_ID_4017}					
	Delete IPBR successfully	${IPBR_ID_4018}					
							
Reserve Iub Leg with IPBR_4017, and the starting port and maximum port of the request equals to PORT_53999	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=1,MUDP=${MUX_LOCAL_PORT_53999},UDP=${MUX_LOCAL_PORT_53999},${EME_PARA}	
	should be equal	${iub_leg_info.result}	SUCCESSFUL				
	Append to List	${IN_LEG_LIST}	${iub_leg_info}				
							
Update the IPBR_4017's local-mux-port from MUX_PORT_53000 to MUX_PORT_53999 with normal mode	${result}	set ip based route with specified parameters	${IPBR_ID_4017}	local-mux-port=${MUX_LOCAL_PORT_53999}	mode=normal		
	should be equal	${result['IPBR Modify']}	Fail				
							
Update the IPBR_4017's local-mux-port from MUX_PORT_53000 to MUX_PORT_53999 with forced mode	${result}	set ip based route with specified parameters	${IPBR_ID_4017}	local-mux-port=${MUX_LOCAL_PORT_53999}	mode=forced		
	should be equal	${result['IPBR Modify']}	Success				
	check specified call should be released						
							
Reserve an IPRO with IPBR_4017 and TEST_IP_ADDRESS_2	Add IPRO successfully	${IPBR_ID_4017}	${IP_GROUP_2.ip_addr}	${IP_GROUP_2.rg_name}	${IP_GROUP_2.iface}		
	${conn_info}	inquiry udp conn resource info	${IP_GROUP_2.ip_addr}	${MUX_LOCAL_PORT_53999}			
							
Reserve an IuB leg with the request port equals to the IPBR_4017's local-mux-port	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=1,MUDP=${MUX_LOCAL_PORT_53000},UDP=${MUX_LOCAL_PORT_53000},${EME_PARA}	
	run keyword if	'${iub_leg_info.result}' == 'SUCCESSFUL'	Append to List	${IN_LEG_LIST}	${iub_leg_info}		
	should be equal	${iub_leg_info.result}	FAILED				
							
Update the IPBR_4017's local-mux-port	${result}	set ip based route with specified parameters	${IPBR_ID_4017}	local-mux-port=${MUX_LOCAL_PORT_53999}			
							
Reserve an IuB leg with the request port equals to the IPBR_4017's original local-mux-port	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=1,MUDP=${MUX_LOCAL_PORT_53000},UDP=${MUX_LOCAL_PORT_53000}	
	run keyword if	'${iub_leg_info.result}' == 'SUCCESSFUL'	Append to List	${IN_LEG_LIST}	${iub_leg_info}		
	should be equal	${iub_leg_info.result}	FAILED				
							
Reserve IuB leg with CAC enable, and the DSCP attribute of the request equals the mux-dscp value of the IPBR_4017	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=0,FMBR=${BITRATE_11000},FABR=${BITRATE_10000},DSCP=${MUX_UDP_DSCP_25_HEX}	
	should be equal	${iub_leg_info.result}	SUCCESSFUL				
	Append to List	${IN_LEG_LIST}	${iub_leg_info}				
	${conn_info}	inquiry udp conn resource info	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}			
	${expect_bw}	evaluate	${BITRATE_11000}*0.2*0.7 + ${BITRATE_10000}*0.8*0.7				
	should be equal as numbers	${expect_bw}	${conn_info['${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}:${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}']['reserve_bw']}				
	[Return]	${iub_leg_info}					
							
update IuB leg bandwidth with multiplexing IP enabled	[Arguments]	${iub_leg_info}					
	${result}	Modify Leg Traffic Parameter	${USCP_0}	${CALL_ID}	${iub_leg_info.leg_id}	10,T::FMBR=${BITRATE_13000},FABR=${BITRATE_12000},NCAC=0	
	should be equal	${result.result}	SUCCESSFUL				
	${conn_info}	inquiry udp conn resource info	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}			
	${expect_bw}	evaluate	${BITRATE_13000}*0.2*0.7 + ${BITRATE_12000}*0.8*0.7				
	should be equal as numbers	${expect_bw}	${conn_info['${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}:${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}']['reserve_bw']}				
							
Reserve IuB leg with CAC enable, and the DSCP attribute of the request does not equal the mux-dscp value of the IPBR_4017	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=0,FMBR=${BITRATE_11000},FABR=${BITRATE_10000},DSCP=${MUX_UDP_DSCP_35}	
	should be equal	${iub_leg_info.result}	SUCCESSFUL				
	Append to List	${IN_LEG_LIST}	${iub_leg_info}				
	${conn_info}	inquiry udp conn resource info	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}	${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}			
	${expect_bw}	evaluate	${BITRATE_11000}*0.2 + ${BITRATE_10000}*0.8				
	should be equal as numbers	${expect_bw}	${conn_info['${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_ip}:${iub_leg_info.legs['${iub_leg_info.leg_id}'].local_port}']['reserve_bw']}				
							
Reserve RTP leg with CAC enable, and the DSCP attribute of the request equals the mux-dscp value of the IPBR_4017	${rtp_leg_info}	create rtp leg	${USCP_0}	${IPBR_ID_4017}	NCAC=0,FMBR=${BITRATE_11000},FABR=${BITRATE_10000},DSCP=${MUX_UDP_DSCP_25}		
	should be equal	${rtp_leg_info.result}	SUCCESSFUL				
	Append to List	${OUT_LEG_LIST}	${rtp_leg_info}				
	${conn_info}	inquiry udp conn resource info	${rtp_leg_info.legs['${rtp_leg_info.leg_id}'].local_ip}	${rtp_leg_info.legs['${rtp_leg_info.leg_id}'].local_port}			
	${expect_bw}	evaluate	${BITRATE_11000}*0.2 + ${BITRATE_10000}*0.8				
	should be equal as numbers	${expect_bw}	${conn_info['${rtp_leg_info.legs['${rtp_leg_info.leg_id}'].local_ip}:${rtp_leg_info.legs['${rtp_leg_info.leg_id}'].local_port}']['reserve_bw']}				
							
reserve IPRO with IPBR mux-setting enable and RTP leg for the test	create IPBR and IPRO with multiplexing parameters successfully						
	create leg list						
	${rtp_leg_info}	create rtp leg	${USCP_0}	${IPBR_ID_1}	NCAC=1		
	should be equal	${rtp_leg_info.result}	SUCCESSFUL				
	Append to List	${OUT_LEG_LIST}	${rtp_leg_info}				
	set test variable	\${CALL_ID}	${rtp_leg_info.call_id}				
	set test variable	\${RTP_LEG}	${rtp_leg_info}				
							
release test resource created in the test case	release legs created in test case						
	Delete IPRO successfully	${IPBR_ID_4017}	${IP_GROUP_1.ip_addr}	${IP_GROUP_1.rg_name}	${IP_GROUP_1.iface}		
	Delete IPBR successfully	${IPBR_ID_4017}					
							
create IPBR and IPRO with multiplexing parameters successfully	Add IPBR successfully	${IPBR_ID_4017}	${IPBR_NAME_4017}	${ROUTE_BW_DEFAULT}	${COMMIT_BW_DEFAULT}	${COMMIT_SIG_BW_DEFAULT}	${COMMIT_DCN_BW_DEFAULT}
	...	${IFC_NRTHSDPA_E_RED}	${IFC_NRTHSDPA_E_RED}	${SCHEDULE_TYPE_DEFAULT}	${PHB_PROFILE_0}	${DSPM_PROFILE_0}	${MUX_FLAG_ENABLE}
	...	${MUX_PKT_NUM_5}	${MUX_LOCAL_PORT_53000}	${MUX_REMOTE_PORT_65534}	${MUX_UDP_DSCP_25}		
	Add IPRO successfully	${IPBR_ID_4017}	${IP_GROUP_1.ip_addr}	${IP_GROUP_1.rg_name}	${IP_GROUP_1.iface}		
							
Modify IPBR with multiplexing parameter successfully	${result}	set ip based route with specified parameters	${IPBR_ID_4017}	local-mux-port=${MUX_LOCAL_PORT_53999}			
	should be equal	${result['IPBR Modify']}	Success				
							
release IPRO and IPBR for the test successfully	delete IP based route resource with IPBR ID	${IPBR_ID_4017}					
							
Connect to Target Hardware and create Test resource	connect to target hardware						
	switch to active OMU						
	${IP_GROUPS}	select random IP address and recovery group	${TWO_GROUP}				
	Set Suite Variable	\${IP_GROUP_1}	${IP_GROUPS[0]}				
	Set Suite Variable	\${IP_GROUP_2}	${IP_GROUPS[1]}				
							
Remove Test Resource and Disconnect from Target Hardware	disconnect from target hardware						
							
Check the MuX port should be updated in CACPRB	${result}	inquiry_udp_conn_port_info	${IP_GROUP_1.ip_addr}	${MUX_LOCAL_PORT_53999}			
	should be equal	${result['${IP_GROUP_1.ip_addr}:${MUX_LOCAL_PORT_53999}']['type']}	MUX				
							
Check the MuX port should be active in CACPRB	${result}	inquiry_udp_conn_port_info	${IP_GROUP_1.ip_addr}	${MUX_LOCAL_PORT_53000}			
	should be equal	${result['${IP_GROUP_1.ip_addr}:${MUX_LOCAL_PORT_53000}']['type']}	MUX				
							
register expect error syslog	register expect syslog	fail to allocate UDP port	cac_res_reserve_ack_s error(0x3531c)	alloc_logi_tt_cac_res: cac_res_reserve_req__r error(0x3531c)	work_rm_ip_res_req_proc: error(0x3531C)	RMUH02: Create call failed. error_code is 217884 (0x3531C)	leg_create_proc: error(0x3531c)
							
reserve an emergency Iub leg with the request port equal to the IPBR_4017's original local-mux-port	${iub_leg_info}	create iub leg	${USCP_0}	${IPBR_ID_4017}	${CALL_ID}	NCAC=1,MUDP=${MUX_LOCAL_PORT_53000},UDP=${MUX_LOCAL_PORT_53000},${EME_PARA}	
	run keyword if	'${iub_leg_info.result}' == 'SUCCESSFUL'	Append to List	${IN_LEG_LIST}	${iub_leg_info}		
	should be equal	${iub_leg_info.result}	SUCCESSFUL				
							
check the original local-mux-port in IP buffer	check ip bitmap in cac	${IP_GROUP_1.ip_addr}					
